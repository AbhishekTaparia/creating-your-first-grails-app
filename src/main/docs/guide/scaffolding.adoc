Now that we have a home page, let's create controllers to manage the domain model we created earlier. Create 3 new controllers for each of the domain classes, `Vehicle`, `Make`, and `Model`.

[source, bash]
----
$ ./grailsw create-controller com.example.Vehicle

| Created grails-app/controllers/com/example/VehicleController.groovy
| Created src/test/groovy/com/example/VehicleControllerSpec.groovy

$ ./grailsw create-controller com.example.Make

| Created grails-app/controllers/com/example/MakeController.groovy
| Created src/test/groovy/com/example/MakeControllerSpec.groovy

$ ./grailsw create-controller com.example.Model

| Created grails-app/controllers/com/example/ModelController.groovy
| Created src/test/groovy/com/example/ModelControllerSpec.groovy
----

We'd like to have actions to allow us to create new domain class instances and persist them to the database. In addition, we'd like to have the ability to edit existing instances and even delete them. Normally all this functionality would require a lot of coding, but Grails gives us a headstart with scaffolding.

TIP: Learn more about scaffolding in the http://docs.grails.org/latest/guide/scaffolding.html[Grails documentation].

To use scaffolding, edit the three controllers we just created, and replace the default `index` action with a `scaffolding` property as shown in the examples below.

[source, groovy]
.grails-app/controllers/com/example/VehicleController.groovy
----
package com.example

class VehicleController {

    static scaffold = Vehicle
}
----

[source, groovy]
.grails-app/controllers/com/example/MakeControler.groovy
----
package com.example

class MakeControler {

    static scaffold = Make
}
----

[source, groovy]
.grails-app/controllers/com/example/ModelController.groovy
----
package com.example

class ModelController {

    static scaffold = Model
}
----

With the `scaffold` property set, Grails will now generate all necessary CRUD (Create, Read, Update, Delete) actions for the respective domain classes. It will also dynamically generate views with list, create, show and edit pages using our domain properties and associations. This can give you a big leg up when putting together the beginnings of an application.

Restart the app, and browse to `http://localhost:8080/vehicle` - you should see a list of the `Vehicle` instances we added to our `BootStrap`. Try out the new views and create, view, edit and delete some instances. You can also do the same with the `Model` and `Make` controllers.

=== Generating the Scaffolding

Dynamic scaffolding is powerful and sometimes will provide all the functionality you need (especially for an administrative site where data access is more important than presentation). But it's quite likely that you will feel the need to customize the generated views and controllers, either to change their appearance or to add custom logic and functionality. Grails anticipates this need and provides a set of `generate` commands that can generate the controllers and/or views that you just saw, allowing you to modify them to suite your needs.

To generate the views (and continue to use the dynamic scaffolding):

[source, bash]
----
$ ./grailsw generate-views com.example.Vehicle
----

To generate the controller (and continue to use the dynamic GSP views):

[source, bash]
----
$ ./grailsw generate-controller com.example.Vehicle
----

To both views and controllers (bypassing all dynamic generation):

[source, bash]
----
$ ./grailsw generate-all com.example.Vehicle
----

The generated controller will be placed under `grails-app/controller`, and the generated views will be placed under `grails-app/views/vehicle`.

TIP: To override existing files, use th `-force` flag along with the `generate-*` command: `./grailsw generate-all com.example.Vehicle -force`

Let's generate the controller and views for `Vehicle` and take a look at the resulting controller.

[source, bash]
----
$ ./grailsw generate-all com.example.Vehicle -force
----

Open the `VehicleController.groovy` file at `grails-app/controller/com/example/`.


[source, groovy]
.grails-app/controller/com/example/VehicleController.groovy
----
package com.example

import static org.springframework.http.HttpStatus.*
import grails.transaction.Transactional

@Transactional(readOnly = true) //<1>
class VehicleController {

    static allowedMethods = [save: "POST", update: "PUT", delete: "DELETE"]

    def index(Integer max) {
        params.max = Math.min(max ?: 10, 100)
        respond Vehicle.list(params), model:[vehicleCount: Vehicle.count()] // <2>
    }

    def show(Vehicle vehicle) {
        respond vehicle //<2>
    }

    def create() {
        respond new Vehicle(params) //<2>
    }

    @Transactional //<1>
    def save(Vehicle vehicle) {
        if (vehicle == null) {
            transactionStatus.setRollbackOnly()
            notFound()
            return
        }

        if (vehicle.hasErrors()) {
            transactionStatus.setRollbackOnly()
            respond vehicle.errors, view:'create'  //<2>
            return
        }

        vehicle.save flush:true

        request.withFormat {  //<3>
            form multipartForm {
                //<4>
                flash.message = message(code: 'default.created.message', args: [message(code: 'vehicle.label', default: 'Vehicle'), vehicle.id])
                redirect vehicle
            }
            '*' { respond vehicle, [status: CREATED] } //<2>
        }
    }

    def edit(Vehicle vehicle) {
        respond vehicle //<2>
    }

    @Transactional //<1>
    def update(Vehicle vehicle) {
        if (vehicle == null) {
            transactionStatus.setRollbackOnly()
            notFound()
            return
        }

        if (vehicle.hasErrors()) {
            transactionStatus.setRollbackOnly()
            respond vehicle.errors, view:'edit' //<2>
            return
        }

        vehicle.save flush:true

        request.withFormat {
            form multipartForm {
                //<4>
                flash.message = message(code: 'default.updated.message', args: [message(code: 'vehicle.label', default: 'Vehicle'), vehicle.id])
                redirect vehicle <5>
            }
            '*'{ respond vehicle, [status: OK] } //<2>
        }
    }

    @Transactional //<1>
    def delete(Vehicle vehicle) {

        if (vehicle == null) {
            transactionStatus.setRollbackOnly()
            notFound()
            return
        }

        vehicle.delete flush:true

        request.withFormat {
            form multipartForm {
                //<4>
                flash.message = message(code: 'default.deleted.message', args: [message(code: 'vehicle.label', default: 'Vehicle'), vehicle.id])
                redirect action:"index", method:"GET" //<5>
            }
            '*'{ render status: NO_CONTENT } //<6>
        }
    }

    protected void notFound() {
        request.withFormat {
            form multipartForm {
                //<4>
                flash.message = message(code: 'default.not.found.message', args: [message(code: 'vehicle.label', default: 'Vehicle'), params.id])
                redirect action: "index", method: "GET" //<5>
            }
            '*'{ render status: NOT_FOUND }  //<6>
        }
    }
}
----
<1> The http://docs.grails.org/latest/api/grails/transaction/Transactional.html[`@Transactional`] annotation configures the transactional behavior of the controller or method. Transactions are used to manage persistence and other complicated operations that should be completed together (and potentially rolled-back if any one of the steps fails).  For more information on transactions, see the http://docs.grails.org/latest/guide/single.html#declarativeTransactions[Grails documentation]
<2> The `respond` method takes an object to return to the requestor, using https://docs.grails.org/latest/guide/theWebLayer.html#contentNegotiation[content negotiation] to choose the correct type (for example, a request's `Accept` header might specify JSON or XML). `respond` also can accept a map of arguments, such as `model` (which defines the way the data is loaded on a page). For more on how to use the `respond` method, see the https://docs.grails.org/latest/ref/Controllers/respond.html[Grails documentation].
<3> `request` is available on all controllers, and is an instance of the Servlet APIâ€™s https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletRequest.html[HttpServletRequest] class. You can access request headers, store properties in the request scope, and get information about the requestor using this object. For more see the https://docs.grails.org/latest/ref/Controllers/request.html[Grails documentation on `request`].
<4> `flash` is a map that stores objects within the session for the *next* request, automatically clearing them after that next request completes. This is useful for passing error messages or other data that you want the next request to access. For more, see the https://docs.grails.org/latest/ref/Controllers/flash.html[Grails documentation on `flash`].
<5> The `redirect` method is a simple one - it allows the action to redirect the request to another action, controller, or a URI. You can also pass along parameters with the redirect. See the https://docs.grails.org/latest/ref/Controllers/redirect.html[Grails documentation on `redirect`] for more.
<6> The `render` method is a less sophisticated version of `respond` - it doesn't perform content negotiation, so you have to specify exactly what you want to render. You can render plain text, a view or template, an HTTP response code, or any object that has a String representation. See the https://docs.grails.org/latest/ref/Controllers/render.html[Grails documentation].

That's a lot of code! Generating and modifying a scaffold controller is a good learning exercise, so feel free to experiment and modify this code - you can always revert back to the version in the `completed` project of this guide.
